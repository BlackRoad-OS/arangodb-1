# -*- mode: CMAKE; -*-

# ------------------------------------------------------------------------------
# External Projects used by ArangoDB
# ------------------------------------------------------------------------------

include(UpdateModule)

remove_definitions("-DUSE_ENTERPRISE=1")

################################################################################
## gtest
################################################################################

UpdateModule(${GIT_EXECUTABLE} "gtest" ${CMAKE_CURRENT_SOURCE_DIR} "README.md")
set(INSTALL_GTEST "OFF")
set(BUILD_GMOCK "ON")
add_subdirectory(gtest)

################################################################################
## ZLIB
################################################################################

set(SKIP_INSTALL_ALL ON)
add_subdirectory(zlib/zlib-1.2.13)
add_library(zlib_interface INTERFACE)
target_include_directories(zlib_interface SYSTEM INTERFACE ${ZLIB_INCLUDE_DIR})

################################################################################
## SNAPPY
################################################################################

function (add_snappy)
  # use the function to open a scope so CMAKE_CXX_FLAGS is only changed for
  # snappy
  add_c_flags_if_supported(CMAKE_C_FLAGS -Wno-suggest-override -Wno-sign-compare)
  add_cxx_flags_if_supported(CMAKE_CXX_FLAGS -Wno-suggest-override -Wno-sign-compare)
  set(SNAPPY_VERSION "1.2.2")
  set(SNAPPY_SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/snappy/snappy-${SNAPPY_VERSION}")
  set(SNAPPY_SOURCE_DIR "${SNAPPY_SOURCE_DIR}" PARENT_SCOPE)
  set(SNAPPY_BUILD_DIR "${CMAKE_CURRENT_BINARY_DIR}/snappy/snappy-${SNAPPY_VERSION}" PARENT_SCOPE)
  set(SNAPPY_LIB "snappy")
  set(SNAPPY_FUZZING_BUILD OFF CACHE BOOL "build Snappy for fuzzing")
  set(SNAPPY_BUILD_TESTS OFF CACHE BOOL "disable Snappy tests" FORCE)
  set(SNAPPY_BUILD_BENCHMARKS OFF CACHE BOOL "disable Snappy benchmarks" FORCE)
  set(SNAPPY_INSTALL OFF CACHE BOOL "disable Snappy installation")
  set(SNAPPY_REQUIRE_AVX ON CACHE BOOL "target processors with AVX support" FORCE)
  set(SNAPPY_REQUIRE_AVX2 OFF CACHE BOOL "target processors with AVX2 support" FORCE)
  set(SNAPPY_HAVE_BMI2 OFF CACHE BOOL "target processors with BMI2 support" FORCE)
  add_subdirectory(${SNAPPY_SOURCE_DIR})
endfunction ()
add_snappy()
set(SNAPPY_SOURCE_DIR "${SNAPPY_SOURCE_DIR}" PARENT_SCOPE)
set(SNAPPY_BUILD_DIR "${SNAPPY_BUILD_DIR}" PARENT_SCOPE)

################################################################################
## LIBUNWIND
################################################################################

if (USE_LIBUNWIND)
  add_subdirectory(libunwind)
  include_directories(SYSTEM "${LIBUNWIND_HOME}/include")
  set(LIBUNWIND_HOME "${LIBUNWIND_HOME}" PARENT_SCOPE)
  set(LIBUNWIND_LIB "${LIBUNWIND_LIB}" PARENT_SCOPE)
endif ()

################################################################################
## JEMALLOC
################################################################################

if (USE_JEMALLOC)
  add_subdirectory(jemalloc)
  include_directories(SYSTEM "${JEMALLOC_HOME}/include")
  link_directories("${JEMALLOC_HOME}/lib")
  set(JEMALLOC_HOME "${JEMALLOC_HOME}" PARENT_SCOPE)
  set(SYS_LIBS ${SYS_LIBS} jemalloc PARENT_SCOPE)
  set(JEMALLOC_LIB "${JEMALLOC_LIB}" PARENT_SCOPE)
  if (USE_JEMALLOC_PROF AND USE_LIBUNWIND)
    add_dependencies(jemalloc_build libunwind_build)
  endif()
endif ()

################################################################################
## V8 and ICU
################################################################################

option(USE_PRECOMPILED_V8 "use a precompiled V8" OFF)

set(V8_VERSION
  "12.1.165"
  CACHE INTERNAL
  "${PROJECT_NAME}: Version"
  FORCE
)
set(V8_VERSION ${V8_VERSION} PARENT_SCOPE)
set(V8_SUB_DIR "v8")
add_subdirectory(v8-build)
add_library(v8_interface INTERFACE)
target_include_directories(v8_interface SYSTEM INTERFACE ${V8_INCLUDE_DIR})

set(ICU_DT "${ICU_DT}" PARENT_SCOPE)
set(ICU_DT_DEST "${ICU_DT_DEST}" PARENT_SCOPE)

set(V8_INTERNAL_INCLUDE_DIR ${V8_INTERNAL_INCLUDE_DIR} PARENT_SCOPE)

################################################################################
## Legacy ICU V 64.2 for index backwards compatibility
################################################################################

add_subdirectory(icu-legacy)
set(ICU64_LIBS "${ICU64_LIBS}" PARENT_SCOPE)
set(ICU64_INCLUDE_DIR "${ICU64_INCLUDE_DIR}" PARENT_SCOPE)
set(ICU64_LIBRARY_DIR "${ICU64_LIBRARY_DIR}" PARENT_SCOPE)
set(ICU_DT_LEGACY "${ICU_DT_LEGACY}" PARENT_SCOPE)
set(ICU_DT_LEGACY_DEST "${ICU_DT_LEGACY_DEST}" PARENT_SCOPE)

################################################################################
## Google Abseil
################################################################################

UpdateModule(${GIT_EXECUTABLE} "abseil-cpp" ${CMAKE_CURRENT_SOURCE_DIR} "README.md")

set(ABSL_PROPAGATE_CXX_STD ON)

add_subdirectory(abseil-cpp)

################################################################################
## Google S2
################################################################################

add_subdirectory(s2geometry)

################################################################################
## BOOST
################################################################################

add_subdirectory(boost)
set(BOOST_VERSION ${BOOST_VERSION} PARENT_SCOPE)

################################################################################
## Velocypack
################################################################################

UpdateModule(${GIT_EXECUTABLE} "velocypack" ${CMAKE_CURRENT_SOURCE_DIR} "README.md")

set(HashType "xxhash" CACHE STRING "Hash type (fasthash, xxhash)" FORCE)
set(BuildVelocyPackExamples OFF CACHE BOOL "Build VelocyPack Examples" FORCE)
set(BuildTools OFF CACHE BOOL "Build VelocyPack Tools" FORCE)
set(UseIPO ${IPO_ENABLED} CACHE STRING "Use interprocedural optimization: ON, OFF or AUTO")

# Define set_ipo macro for use in this file
if (IPO_ENABLED)
  macro(SET_IPO _target)
    set_property(TARGET ${_target} PROPERTY INTERPROCEDURAL_OPTIMIZATION ${IPO_ENABLED})
  endmacro()
else ()
  macro(SET_IPO _target)
    # NOOP
  endmacro()
endif ()

add_subdirectory(velocypack)

################################################################################
## ROCKSDB
################################################################################

UpdateModule(${GIT_EXECUTABLE} "rocksdb" ${CMAKE_CURRENT_SOURCE_DIR} "README.md")

add_subdirectory(rocksdb)
add_library(rocksdb_interface INTERFACE)
target_include_directories(rocksdb_interface SYSTEM INTERFACE ${PROJECT_SOURCE_DIR}/3rdParty/rocksdb/include)
target_include_directories(rocksdb_interface SYSTEM INTERFACE ${PROJECT_SOURCE_DIR}/3rdParty/rocksdb)
list(APPEND LINK_DIRECTORIES "${CMAKE_CURRENT_BINARY_DIR}/rocksdb")

################################################################################
# llhttp
################################################################################

SET(LLHTTP_SOURCES
  ${CMAKE_CURRENT_SOURCE_DIR}/llhttp/src/api.c
  ${CMAKE_CURRENT_SOURCE_DIR}/llhttp/src/http.c
  ${CMAKE_CURRENT_SOURCE_DIR}/llhttp/src/llhttp.c
)

add_library(llhttp STATIC
  ${LLHTTP_SOURCES}
)

target_include_directories(llhttp PUBLIC "${CMAKE_CURRENT_SOURCE_DIR}/llhttp/include")

################################################################################
## NGHTTP2
################################################################################

add_subdirectory(nghttp2-lib)

################################################################################
## IMMER
################################################################################

UpdateModule(${GIT_EXECUTABLE} "immer" ${CMAKE_CURRENT_SOURCE_DIR} "README.rst")
set(immer_BUILD_EXAMPLES    OFF CACHE BOOL "don't build examples")
set(immer_BUILD_EXTRAS    OFF CACHE BOOL "don't build extras")
set(immer_BUILD_TESTS OFF CACHE BOOL "don't build tests")
set(immer_BUILD_DOCS OFF CACHE BOOL "don't docs")
add_subdirectory(immer)

################################################################################
## DATE
################################################################################

add_library(date_interface INTERFACE)
target_include_directories(date_interface SYSTEM INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/date/include)

################################################################################
## Linenoise
################################################################################

add_library(linenoise-ng STATIC
  linenoise-ng/src/ConvertUTF.cpp
  linenoise-ng/src/linenoise.cpp
  linenoise-ng/src/wcwidth.cpp)

target_include_directories(linenoise-ng SYSTEM PUBLIC
  ${PROJECT_SOURCE_DIR}/3rdParty/linenoise-ng/include)

################################################################################
## fmt
################################################################################

UpdateModule(${GIT_EXECUTABLE} "fmt" ${CMAKE_CURRENT_SOURCE_DIR} "README.rst")
add_subdirectory(fmt)

################################################################################
## LINK_DIRECTORIES
################################################################################

set(LINK_DIRECTORIES "${LINK_DIRECTORIES}" PARENT_SCOPE)

################################################################################
## fuerte
################################################################################

add_subdirectory(fuerte)

################################################################################
## taojson
################################################################################

set(TAOCPP_JSON_BUILD_TESTS    OFF CACHE BOOL "Build taocpp::json test programs"    FORCE)
set(TAOCPP_JSON_BUILD_EXAMPLES OFF CACHE BOOL "Build taocpp::json example programs" FORCE)
add_subdirectory(taocpp-json)
add_subdirectory(json-schema-validation)

################################################################################
## tzdata
################################################################################

LIST(APPEND TZ_DATA_FILES
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/africa"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/antarctica"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/asia"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/australasia"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/backward"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/backzone"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/calendars"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/etcetera"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/europe"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/NEWS"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/northamerica"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/southamerica"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/version"
  "${CMAKE_CURRENT_SOURCE_DIR}/tzdata/windowsZones.xml")

set(TZ_DATA_FILES ${TZ_DATA_FILES} PARENT_SCOPE)

add_custom_target(tzdata)

if (MSVC)
  add_custom_command(
    TARGET tzdata POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_BINARY_DIR}/bin/$<CONFIG>/tzdata
    COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_BINARY_DIR}/tests/$<CONFIG>/tzdata
    COMMAND ${CMAKE_COMMAND} -E copy ${TZ_DATA_FILES} ${CMAKE_BINARY_DIR}/bin/$<CONFIG>/tzdata
    COMMAND ${CMAKE_COMMAND} -E copy ${TZ_DATA_FILES} ${CMAKE_BINARY_DIR}/tests/$<CONFIG>/tzdata)
else()
  add_custom_command(
    TARGET tzdata POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_BINARY_DIR}/bin/tzdata
    COMMAND ${CMAKE_COMMAND} -E make_directory ${CMAKE_BINARY_DIR}/tests/tzdata
    COMMAND ${CMAKE_COMMAND} -E copy ${TZ_DATA_FILES} ${CMAKE_BINARY_DIR}/bin/tzdata
    COMMAND ${CMAKE_COMMAND} -E copy ${TZ_DATA_FILES} ${CMAKE_BINARY_DIR}/tests/tzdata)
endif()

### Suppressing all warnings from external libs
if (SUPPRESS_EXTERNAL_WARNINGS)
  if (MSVC)
    add_compile_options(/W0)
  else ()
    add_compile_options(-Wno-all)
  endif ()
endif ()

################################################################################
### Highway library
################################################################################

set(BUILD_TESTING OFF CACHE INTERNAL "" FORCE)
add_subdirectory(highway)

################################################################################
### sse2neon
### need this after highway and before simdcomp
################################################################################

add_library(sse2neon INTERFACE)

if (ARCH_AARCH64)
  set(SSE2NEON_ROOT ${CMAKE_CURRENT_SOURCE_DIR}/sse2neon)

  target_include_directories(
    sse2neon
    SYSTEM
    INTERFACE ${SSE2NEON_ROOT}
  )
endif ()

################################################################################
### OpenFST + Kaldi library
################################################################################

# We build OpenFST extensions provided by Kaldi as a part of OpenFST
set(Kaldi_sources
  ${CMAKE_CURRENT_SOURCE_DIR}/kaldi/src/base/io-funcs.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/kaldi/src/base/kaldi-error.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/kaldi/src/base/kaldi-utils.cc
  )

set(OpenFST_sources
  ${CMAKE_CURRENT_SOURCE_DIR}/openfst/compat.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/openfst/encode.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/openfst/flags.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/openfst/fst.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/openfst/fst-types.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/openfst/mapped-file.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/openfst/properties.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/openfst/symbol-table.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/openfst/symbol-table-ops.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/openfst/util.cc
  ${CMAKE_CURRENT_SOURCE_DIR}/openfst/weight.cc
  ${Kaldi_sources}
  )

add_library(
  iresearch-ofst
  OBJECT
  ${OpenFST_sources}
)

set_ipo(iresearch-ofst)

# disable warnings for 3rd-party libs for a cleaner build
if (MSVC)
  target_compile_options(
    iresearch-ofst
    # MSVC2015-2017 require "/bigobj" to compile debug build
    PRIVATE "$<$<CONFIG:Debug>:/bigobj>"
    PRIVATE "/W0"
  )
else ()
  CHECK_C_COMPILER_FLAG(-fPIC SUPPORT_FPIC)
  if (SUPPORT_FPIC)
    target_compile_options(
      iresearch-ofst
      PRIVATE "-fPIC"
    )
  endif ()
  target_compile_options(
    iresearch-ofst
    PRIVATE "-Wno-sign-compare"
    PRIVATE "-Wno-missing-field-initializers"
  )
endif ()

# force c++17 for ofst as transitional step
set_target_properties(iresearch-ofst
  PROPERTIES
  CXX_STANDARD 17
  CXX_STANDARD_REQUIRED YES
  )

target_include_directories(
  iresearch-ofst
  SYSTEM
  PUBLIC ${CMAKE_CURRENT_SOURCE_DIR}/openfst
  PUBLIC ${CMAKE_CURRENT_SOURCE_DIR}/kaldi/src
)

################################################################################
### SIMDCOMP library
################################################################################

add_subdirectory(simdcomp)

################################################################################
### frozen library
################################################################################

set(FROZEN_INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/frozen/include)
set(FROZEN_INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/frozen/include PARENT_SCOPE)

################################################################################
### abseil-cpp library
################################################################################
if (NOT TARGET absl::base)
  if ("${ABSL_ROOT}" STREQUAL "")
    set(ABSL_ROOT "$ENV{ABSL_ROOT}")
  endif ()

  if ("${ABSL_ROOT}" STREQUAL "")
    message("ABSL_ROOT not set. Pulling abseil-cpp submodule.")
    UpdateModule(${GIT_EXECUTABLE} "external/abseil-cpp" "${CMAKE_CURRENT_SOURCE_DIR}/.." "README.md")
    set(ABSL_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/../external/abseil-cpp")
  endif ()

  if ("${ABSL_ROOT}" STREQUAL "")
    message(FATAL_ERROR "ABSL_ROOT not set")
  else ()
    set(CMAKE_CXX_STANDARD_REQUIRED ON CACHE BOOL "" FORCE)
    set(ABSL_BUILD_TESTING OFF CACHE BOOL "" FORCE)
    set(ABSL_PROPAGATE_CXX_STD ON CACHE BOOL "" FORCE)
    set(BUILD_SHARED_LIBS OFF CACHE BOOL "" FORCE)
    set(ABSL_USE_SYSTEM_INCLUDES ON CACHE BOOL "" FORCE)
    expand_path(${ABSL_ROOT} ABSL_ROOT_EXP)
    add_subdirectory(${ABSL_ROOT_EXP}
      ${CMAKE_CURRENT_BINARY_DIR}/CMakeFiles/iresearch-abseil-cpp.dir
      EXCLUDE_FROM_ALL)
  endif ()
endif ()

################################################################################
### velocypack library
################################################################################
if (NOT TARGET velocypack)
  if ("${VPACK_ROOT}" STREQUAL "")
    set(VPACK_ROOT "$ENV{VPACK_ROOT}")
  endif ()

  if ("${VPACK_ROOT}" STREQUAL "")
    message("VPACK_ROOT not set. Pulling velocypack submodule.")
    UpdateModule(${GIT_EXECUTABLE} "external/velocypack" "${CMAKE_CURRENT_SOURCE_DIR}/.." "README.md")
    set(VPACK_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/../external/velocypack")
  endif ()

  if ("${VPACK_ROOT}" STREQUAL "")
    message(FATAL_ERROR "VPACK_ROOT not set")
  else ()
    expand_path(${VPACK_ROOT} VPACK_ROOT_EXP)
    add_subdirectory(${VPACK_ROOT_EXP}
      ${CMAKE_CURRENT_BINARY_DIR}/CMakeFiles/iresearch-velocypack.dir
      EXCLUDE_FROM_ALL)
  endif ()
endif ()

################################################################################
### Boost::Text library
################################################################################
# Force C++17 for boost::text
set(CXX_STD 17 CACHE STRING "Set to 14, 17, etc., to enable C++14, C++17, etc." FORCE)
add_subdirectory(text)
# force c++17
set_target_properties(text
  PROPERTIES
  CXX_STANDARD 17
  CXX_STANDARD_REQUIRED YES
  )

################################################################################
### fastText library
################################################################################

add_subdirectory(fastText)

################################################################################
### tests only library
################################################################################

if (USE_UTILS OR USE_TESTS OR USE_MICROBENCH OR USE_PYRESEARCH)
  add_subdirectory(tests)
endif ()
